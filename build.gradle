description = "Fully autonomous algorithmic trading environment."

buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        maven { url 'http://clojars.org/repo' }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url 'http://repo.spring.io/plugins-release' }
    }
    dependencies {
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.0.1'
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.7.6'
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.3.3.RELEASE'
    }
}

// Global =======================================================================================

subprojects {
    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'com.google.protobuf'

    apply plugin: 'idea'

    group = "jarvis" // we don't need the domain

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    repositories {
        mavenCentral()
        mavenLocal()

        maven { url 'http://clojars.org/repo' }
        // open hft snapshots
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    }

    dependencies {
        // kotlin
        compile 'org.jetbrains.kotlin:kotlin-stdlib:1.0.1'
        compile "org.jetbrains:annotations:15.0"

        // protobuf
        compile 'com.google.protobuf:protobuf-java:3.0.0-beta-2'
        compile 'com.google.protobuf:protobuf-java-util:3.0.0-beta-2'

        compile 'io.grpc:grpc-all:0.13.2'

        // rx java
        compile 'io.reactivex:rxjava:1.1.3'

        // java 8 stream utils
        compile 'one.util:streamex:0.5.5'

        // functional
        compile 'com.javaslang:javaslang:1.2.2'

        // utils
        compile 'com.google.guava:guava:19.0'
        compile 'com.google.code.gson:gson:2.6.2'
        compile 'org.apache.commons:commons-lang3:3.4'
        compile 'org.apache.commons:commons-io:1.3.2'

        // logging
        compile('org.slf4j:slf4j-api:1.7.19') { force = true }
        compile('org.slf4j:slf4j-log4j12:1.7.19') { force = true }
        compile('org.slf4j:jcl-over-slf4j:1.7.19') { force = true }
        compile('org.slf4j:jul-to-slf4j:1.7.21') { force = true }
        runtime('log4j:log4j:1.2.17') { force = true }

        // test
        testCompile "org.jetbrains.kotlin:kotlin-test:1.0.1"
        testCompile 'junit:junit:4.12'
    }

    // For cache invalidation
    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }

    // Remove unwanted logger implementation from all transitive dependencies
    configurations.all.collect {
        it.exclude group: 'commons-logging'
        it.exclude group: 'ch.qos.logback'
    }

    protobuf {
        generatedFilesBaseDir = "${projectDir}/src/gen"

        plugins {
            grpc {
                artifact = 'io.grpc:protoc-gen-grpc-java:0.13.2'
            }
        }

        protoc {
            artifact = "com.google.protobuf:protoc:3.0.0-beta-2"
        }

        generateProtoTasks {
            all().each { task ->
                task.plugins {
                    grpc {}
                }

                if (task.name.equals("generateProto")) {
                    task.outputs.upToDateWhen {
                        new File(generatedFilesBaseDir).exists()
                    }
                }
            }

            compileJava.dependsOn(generateProto)
            compileKotlin.dependsOn(generateProto)


            task deleteGeneratedProto << {
                delete generatedFilesBaseDir
            }

            task recompileProto {}

            recompileProto.dependsOn(deleteGeneratedProto)
            recompileProto.dependsOn(generateProto)

            clean.dependsOn(deleteGeneratedProto)
        }

    }

    sourceSets {
        main {
            proto {
                srcDir 'src/main/proto'
            }
            java {
                srcDirs 'src/main/java', 'src/gen/main/java', 'src/gen/main/grpc'
            }
            kotlin {
                srcDirs 'src/main/kotlin'
            }
        }
        test {
            proto {
                srcDir 'src/test/proto'
            }
            java {
                srcDirs 'src/test/java', 'src/gen/test/java', 'src/gen/test/grpc'
            }
            kotlin {
                srcDirs 'src/test/kotlin'
            }
        }
    }

    task 'create-dirs' << {
        description = 'Initializes directory structure according to source sets'
        sourceSets.all { set ->
            set.allSource.srcDirs.each { it.mkdirs() }
        }
    }

}

// Util =========================================================================================

project(":util") {

    description = "[lib] Global utilities for java/kotlin development (nothing jarvis specific)."

    // For now we push all dependencies to global project (will split them later)
    dependencies {
        // email
        compile 'net.sargue:mailgun:1.0.0'

        // http
        compile 'org.apache.httpcomponents:httpclient:4.5.1'
        compile 'org.apache.httpcomponents:fluent-hc:4.5.1'
        compile 'org.apache.httpcomponents:httpasyncclient:4.0'

        // websocket
        compile 'javax.websocket:javax.websocket-api:1.0'
        compile 'org.eclipse.jetty.websocket:javax-websocket-client-impl:9.2.7.v20150116'
        compile 'org.eclipse.jetty.websocket:javax-websocket-server-impl:9.2.7.v20150116'

        // wamp
        compile 'org.javassist:javassist:3.20.0-GA'
        compile 'ws.wamp.jawampa:jawampa-netty:0.4.1'

        // pusher
        compile 'com.pusher:pusher-java-client:1.0.0'
    }
}

// Common =======================================================================================

project(":common") {

    description = "[lib] Common jarvis codebase shared across multiple modules."

    dependencies {
        compile project(":util")
    }
}

// Event Store ==================================================================================

project(":eventstore-client") {

    description = "[lib] Event Store GRPC client."

    dependencies {
        compile project(":common")
    }
}

project(":eventstore-server") {

    description = "[app] Event Store server."

    dependencies {
        compile project(":common")
        compile project(":eventstore-client")

        // chronicle
        compile 'net.openhft:chronicle:3.6.0'
    }
}

// Indexer ======================================================================================

project(":indexer") {

    description = "[app] Indexes collected data for querying."

    dependencies {
        compile project(":common")
        compile project(":eventstore-client")
    }
}

// Engine =======================================================================================

project(":engine") {
    apply plugin: 'spring-boot'

    description = "[app] Uses collector services to get realtime exchange data to generate trade signals."

    dependencies {
        compile project(":common")

        compile 'org.springframework.boot:spring-boot-starter'
        compile 'org.springframework.boot:spring-boot-starter-log4j'
    }
}

// Bitfinex =====================================================================================

project(":bitfinex-client") {
    apply plugin: 'spring-boot'

    description = "[lib] Bitfinex client library to work with bitfinex exchange."

    dependencies {
        compile project(":common")
    }

}

project(":bitfinex-collector-client") {
    apply plugin: 'spring-boot'

    description = "[lib] Bitfinex collector client library allows to connect and work with collector service."

    dependencies {
        compile project(":common")
    }

}

project(":bitfinex-collector-server") {
    apply plugin: 'spring-boot'

    description = "[app] Bitfinex collector server collects trade and orderbook data to a persistent store."

    dependencies {
        compile project(":bitfinex-client")
        compile project(":eventstore-client")

        compile 'org.springframework.boot:spring-boot-starter'
        compile 'org.springframework.boot:spring-boot-starter-log4j'
    }

}

task wrapper(type: Wrapper) {
    description = 'Regenerates the Gradle wrapper script'
    gradleVersion = '2.12'
}